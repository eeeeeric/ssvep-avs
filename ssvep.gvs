# The desired frequency for the left/right side of the clip.
# The output is 60 fps by default, so for best results, you will want to choose a 
# frequency that is a factor of 30.
FPS=60
left_freq  = 6
right_freq = 10

# Calculate how many frames to display before switching colors
num_frames_left  = FPS/(left_freq*2)
num_frames_right = FPS/(right_freq*2)

# Initialize empty clips for appending
left_clip = BlankClip(length=0, width=960, height=1080, fps=FPS, color=$FFFFFF)
right_clip = BlankClip(length=0, width=960, height=1080, fps=FPS, color=$FFFFFF)

# Generate FPS frames worth of output
for (i = 0, left_freq*2-1) {
    left_clip = left_clip + BlankClip(length=num_frames_left, width=960, height=1080, fps=FPS, color=(i%2 == 0) ? $FFFFFF : $000000)
}
for (i = 0, right_freq*2-1) {
    right_clip = right_clip + BlankClip(length=num_frames_right, width=960, height=1080, fps=FPS, color=(i%2 == 0) ? $FFFFFF : $000000)
}

# Stack the left and right eyes together
final = StackHorizontal(left_clip, right_clip)

# Add epilepsy warning with countdown (5 seconds)
epilepsy_warning = BlankClip(length=300, width=1920, height=1080, fps=FPS, color=$000000).Subtitle( \
        "EPILEPSY WARNING!\n\n\n" + \
        "A few people may experience epileptic seizures when viewing flashing lights.", \
        size=48, align=5, lsp=10)\
        .Subtitle("5", align=8, size=72, first_frame=0, last_frame=FPS-1)\
        .Subtitle("4", align=8, size=72, first_frame=FPS, last_frame=FPS*2-1)\
        .Subtitle("3", align=8, size=72, first_frame=FPS*2, last_frame=FPS*3-1)\
        .Subtitle("2", align=8, size=72, first_frame=FPS*3, last_frame=FPS*4-1)\
        .Subtitle("1", align=8, size=72, first_frame=FPS*4)

# Loop forever (almost). Use `Loop(final, repetitions)` if you want to restrict
# the number of loops. Each loop lasts 1 second.
epilepsy_warning + Loop(final, 5)